RULE Connector_Zapier_Outbound

//
// Listens for any control action messages
//
WHEN PUBLISH OCCURS ON "/system/adapter/outbound/*" AS m

//
// The action determines which target URL to use
// The payload for the action is the body that is sent to the target URL
//
// Note that Zapier always expects the payload to be an array.
//
var action  = m.newValue.system + "_" + m.newValue.action
var payload = m.newValue.payload

//
// Look to see if the action has been registered.  If so, lookup the 
// Zapier target URL used to post messages.  If none registered, then
// ignore the message.
//
var zapConfig = SELECT ONE * FROM Connector_Zapier_Config WHERE user == username() AND action == action
if(zapConfig) {

    //
    // The Base URI in the Zapier source is "https://zapier.com/" (or similar) so
    // we need to strip out this from the target_url to create the path.  Note
    // that it may be "https://hooks.zapier.com", but the path still works
    // with "https://zapier.com".
    //
    var publishConfig = {
        path: zapConfig.target_url.replace("^https?://[^/]*/", "/")
    }

    PUBLISH { body: payload } TO SOURCE Zapier USING publishConfig

}