RULE Connector_Zapier_Outbound

WHEN PUBLISH OCCURS ON "/system/connector/Zapier/outbound" AS m

//
// The action determines which target URL to use
// The payload for the action is the body that is sent to the target URL
//
// Note that Zapier always expects the payload to be an array.
//
var action  = m.newValue.action
var payload = m.newValue.payload

//
// Lookup Zapier target URL used to post messages
//
var zapConfig = SELECT ONE * FROM Connector_Zapier_Config WHERE user == username() AND action == action
if(zapConfig) {

    //
    // The Base URI in the Zapier source is "https://zapier.com/" so
    // we need to strip out this from the target_url to create the
    // path.
    //
    var publishConfig = {
        path: zapConfig.target_url.substring(length("https://zapier.com/"))
    }

    PUBLISH { body: payload } TO SOURCE Zapier USING publishConfig

} else {
    exception("io.vantiq.system.adapter.zapier.missing.target_url",
              "No target URL registered for user " + username())
}